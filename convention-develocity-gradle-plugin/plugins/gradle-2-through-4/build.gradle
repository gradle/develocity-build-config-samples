plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

// CHANGE ME: change to your organization's group ID
group = 'com.myorg'
version = '1.0'

repositories {
    gradlePluginPortal()
}

dependencies {
    // must not be newer than 1.16 to be compatible with Gradle 2.x - 4.x
    implementation "com.gradle:build-scan-plugin:1.16"
    implementation 'com.gradle:common-custom-user-data-gradle-plugin:2.4.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

gradlePlugin {
    plugins {
        develocityConventions {
            // CHANGE ME: change for your organization
            id = 'com.myorg.convention-develocity-gradle-2-through-4'
            displayName = 'Develocity Convention Plugin for Gradle 2.0 through Gradle 4.10.3'
            description = 'A Gradle plugin to apply and configure the Build Scan Gradle plugin for com.myorg'
            implementationClass = 'com.myorg.ConventionDevelocityGradlePlugin'
        }
    }
}

tasks.withType(ValidatePlugins).configureEach {
    failOnWarning = true
    enableStricterValidation = true
}
