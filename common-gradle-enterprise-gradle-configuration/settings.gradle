plugins {
    id 'com.gradle.enterprise' version '3.10.1'
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '1.6.5'
}

def isCI = System.getenv('CI') != null // adjust to your CI provider

gradleEnterprise {
    server = 'https://enterprise-samples.gradle.com' // adjust to your GE server
    allowUntrustedServer = false // ensure a trusted certificate is configured

    buildScan {
        capture { taskInputFiles = true }
        uploadInBackground = !isCI
        publishAlways()
    }
}

buildCache {
    local {
        enabled = true
    }

    remote(HttpBuildCache) {
        url = 'https://enterprise-samples.gradle.com/cache/' // adjust to your GE server, and note the trailing slash
        allowUntrustedServer = false // ensure a trusted certificate is configured
        credentials { creds ->
            creds.username = System.getenv('GRADLE_ENTERPRISE_CACHE_USERNAME')
            creds.password = System.getenv('GRADLE_ENTERPRISE_CACHE_PASSWORD')
        }
        enabled = true
        push = isCI
    }
}

rootProject.name = 'common-gradle-enterprise-gradle-configuration' // adjust to your project
