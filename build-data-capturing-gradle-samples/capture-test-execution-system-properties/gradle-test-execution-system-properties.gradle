import java.nio.charset.StandardCharsets
import java.security.MessageDigest

/**
 * This Gradle script captures the system properties available to each Test task, hashes the properties' values,
 * and adds these as custom values.
 * This should be applied to the root project since it configures all projects:
 * <code> apply from: file('gradle-test-execution-system-properties.gradle') </code>
 */

def buildScanApi = project.extensions.findByName('buildScan')
if (!buildScanApi) {
    return
}

allprojects {
    tasks.withType(Test).configureEach { t ->
        doFirst {
            t.systemProperties.forEach { key, val -> buildScan.value("${t.identityPath}#sysProps-$key", hash(val)) }
        }
    }
}

ext.MESSAGE_DIGEST = MessageDigest.getInstance('SHA-256')

String hash(Object value) {
    if (value == null) {
        null
    } else {
        String string = String.valueOf(value)
        byte[] encodedHash = MESSAGE_DIGEST.digest(string.getBytes(StandardCharsets.UTF_8))
        StringBuilder hexString = new StringBuilder()
        for (int i = 0; i < encodedHash.length / 4; i++) {
            String hex = Integer.toHexString(0xff & encodedHash[i])
            if (hex.length() == 1) {
                hexString.append('0')
            }
            hexString.append(hex)
        }
        hexString.append('...')
        hexString.toString()
    }
}
