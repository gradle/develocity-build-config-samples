
/**
 * This Gradle script captures any dependency configurations that are resolved early to build the task graph,
 * and adds these as custom values.
 * This should be applied to the root project:
 * <code> apply from: file('gradle-dependency-resolution.gradle') </code>
 */

def buildScanApi = project.extensions.findByName('buildScan')
if (!buildScanApi) {
    return
}

buildScan {
    buildFinished {
        def confs = []
        allprojects { p ->
            p.configurations.each { c ->
                if (c.state == Configuration.State.RESOLVED && c.resolutionStrategy.resolveGraphToDetermineTaskDependencies()) {
                    confs.add c.identityPath
                }
            }
        }

        confs.each { conf ->
            buildScan.value "Configuration resolved for task graph calculation", conf as String
        }
    }
}
